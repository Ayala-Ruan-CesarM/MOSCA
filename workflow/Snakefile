import pathlib
import pandas as pd
import shutil
import sys
from mosca_tools import perform_alignment, generate_expression_matrix, make_entry_report, make_protein_report

scripts_dir = sys.path[0]
exps = pd.DataFrame(config["experiments"])

def set_name(files, data_type):
    filename = files.split('/')[-1]
    if data_type == 'protein':
        return '_'.join(filename.split('_')[:-1])
    if ',' in files:
        return filename.split(',')[0].split('_R')[0]
    return filename.split('.fa')[0]

for i in range(len(exps)):
    if pd.isnull(exps.iloc[i]['Name']) or exps.iloc[i]['Name'] == '':
        exps.iloc[i, exps.columns.get_loc('Name')] = set_name(
            exps.iloc[i]['Files'], exps.iloc[i]['Data type'])
    if not config['do_assembly']:
        exps.iloc[i]['Sample'] = exps.iloc[i]['Name']

pathlib.Path(f"{config['output']}").mkdir(parents=True, exist_ok=True)
exps.to_csv(f"{config['output']}/exps.tsv", sep = '\t', index = False)

mg_exps = exps[exps["Data type"] == 'dna']
mt_exps = exps[exps["Data type"] == 'mrna']
if len(mg_exps) == 0 and len(mt_exps) != 0:
    mg_exps = mt_exps
not_mp_exps = exps[exps["Data type"] != 'protein']

def all_input(wildcards):
    if config['do_assembly']:
        return (
            expand("{output}/MOSCA_Protein_Report.xlsx", output=config["output"]) +
            expand("{output}/MOSCA_Entry_Report.xlsx", output=config["output"]) +
            expand("{output}/technical_report.tsv", output=config["output"]) +
            expand("{output}/MOSCA_General_Report.xlsx", output=config["output"]) +
            expand("{output}/MOSCA_results.zip", output=config["output"]) +
            expand("{output}/Binning/{sample}/checkm.tsv", output=config["output"], sample=set(exps['Sample'])) +
            expand("{output}/KEGG_maps/{sample}/KEGGCharter_results.tsv", output=config["output"],
                sample=set(exps['Sample']))
        )
    else:
        return f"{config['output']}/MOSCA_Entry_Counts_Report.xlsx"

def preprocess_input(wildcards):
    # get first value (in case multiple) and split on commas
    return exps.loc[exps['Name'] == wildcards.name, 'Files'].iloc[0].split(',')

def join_reads_input(wildcards):
    df = mg_exps[mg_exps['Sample'] == wildcards.sample].reset_index()
    return [f'{config["output"]}/Preprocess/Trimmomatic/quality_trimmed_{df.iloc[i]["Name"]}{fr}.fq'
           for i in range(len(df))
           for fr in (['_forward_paired', '_reverse_paired'] if ',' in df.iloc[i]["Files"] else [''])]

def fastq2fasta_input(wildcards):
    return expand("{output}/Preprocess/Trimmomatic/quality_trimmed_{name}{fr}.fq", output=config["output"],
        fr=(['_forward_paired', '_reverse_paired'] if exps["Files"].str.contains(',').tolist() else ''),
        name = wildcards.sample)

def annotation_input(wildcards):
    if config['do_assembly']:
        return expand(
            "{output}/Assembly/{sample}/scaffolds.fasta", output = config["output"],
            sample = set(exps['Sample']))
    return expand(
        "{output}/Preprocess/piled_{name}.fasta", output = config["output"], name = wildcards.sample)

def upimapi_input(wildcards):
    if config['do_assembly']:
        return expand(
            "{output}/Annotation/{sample}/aligned.blast", output=config["output"], sample=set(exps['Sample']))
    return expand(
        "{output}/Annotation/{name}/aligned.blast", output=config["output"], name=set(exps['Name']))

rule all:
    input:
        all_input

rule preprocess:
    input:
        preprocess_input
    output:
        expand("{output}/Preprocess/Trimmomatic/quality_trimmed_{{name}}{fr}.fq", output = config["output"],
            fr = (['_forward_paired', '_reverse_paired'] if exps["Files"].str.contains(',').tolist() else ''))
    threads:
        config["threads"]
    run:
        shell("python {scripts_dir}/preprocess.py -i {reads} -t {threads} -o {output}/Preprocess "
              "-d {data_type} -rd {resources_directory} -n {wildcards.name} --minlen {minlen} --avgqual {avgqual}",
            output = config["output"], reads = ",".join(input), resources_directory = config["resources_directory"],
            data_type = exps.loc[exps['Name'] == wildcards.name]["Data type"].iloc[0],
            minlen = config["minimum_read_length"], avgqual = config["minimum_read_average_quality"])

rule join_reads:
    input:
        join_reads_input
    output:
        expand("{output}/Preprocess/{{sample}}{fr}.fastq", output = config["output"],
            fr = (['_forward', '_reverse'] if exps["Files"].str.contains(',').tolist() else ''))
    threads: 1
    run:
        for file in input:
            print(file)
            if 'forward' in file:
                shell("touch {output}/Preprocess/{wildcards.sample}_forward.fastq; cat {file} >> "
                      "{output}/Preprocess/{wildcards.sample}_forward.fastq", output = config["output"])
            elif 'reverse' in file:
                shell("touch {output}/Preprocess/{wildcards.sample}_reverse.fastq; cat {file} >> "
                      "{output}/Preprocess/{wildcards.sample}_reverse.fastq", output = config["output"])
            else:
                shell("touch {output}/Preprocess/{wildcards.sample}.fastq; cat {file} >> "
                      "{output}/Preprocess/{wildcards.sample}.fastq", output = config["output"])

rule assembly:
    input:
        expand("{output}/Preprocess/{sample}{fr}.fastq", output = config["output"], sample = set(exps['Sample']),
            fr = (['_forward', '_reverse'] if exps["Files"].str.contains(',').tolist() else ''))
    output:
        expand("{output}/Assembly/{sample}/contigs.fasta", output = config["output"],
            sample = set(exps['Sample'])),
        expand("{output}/Assembly/{sample}/scaffolds.fasta", output = config["output"],
            sample = set(exps['Sample']))
    threads:
        config["threads"]
    run:
        reads = ",".join(input)
        shell("python {scripts_dir}/assembly.py -r {reads} -t {threads} -o {output}/Assembly/{sample} -a {assembler} "
              "-m {max_memory}",
            output = config["output"], sample = set(exps['Sample']), assembler = config["assembler"],
            max_memory = config["max_memory"])

rule binning:
    input:
        reads = expand("{output}/Preprocess/{sample}{fr}.fastq", output = config["output"],
            sample = set(exps['Sample']),
            fr = (['_forward', '_reverse'] if exps["Files"].str.contains(',').tolist() else '')),
        contigs = expand("{output}/Assembly/{sample}/scaffolds.fasta", output = config["output"],
            sample = set(exps['Sample']))
    output:
        expand("{output}/Binning/{sample}/checkm.tsv", output = config["output"], sample = set(exps['Sample']))
    threads:
        config["threads"]
    run:
        reads = ",".join(input.reads)
        shell("python {scripts_dir}/binning.py -c {input.contigs} -t {threads} -o {output}/Binning/{sample} -r {reads} "
              "-mset {markerset}{iterative_binning}",
              output = config["output"], markerset = config["markerset"], sample = set(exps['Sample']),
              iterative_binning = ' --iterative-binning' if config['do_iterative_binning'] else '')

rule fastq2fasta:
    input:
        fastq2fasta_input
    output:
        f"{config['output']}/Preprocess/piled_{{sample}}.fasta"
    threads:
        1
    shell:
        "cat {input} | paste - - - - | cut -f 1,2 | sed 's/^@/>/' | tr '\\t' '\\n' > {output}"

rule annotation:
    input:
        annotation_input
    output:
        expand("{output}/Annotation/{{sample}}/fgs.faa", output = config["output"]),
        expand("{output}/Annotation/{{sample}}/fgs.ffn", output = config["output"]),
        expand("{output}/Annotation/{{sample}}/UPIMAPI_results.tsv", output = config["output"])
    threads:
        config["threads"]
    run:
        if not config['do_assembly']:
            input = ",".join(input)
        shell('python {scripts_dir}/annotation.py -i {input} -t {threads} -o {output}/Annotation/{wildcards.sample} '
              '-rd {rd} -em {error_model} -db {upimapi_database} -mts {max_target_seqs}{assembled}{taxids} '
              '-cols "{cols}" -dbs "{dbs}"',
              output = config["output"],
              rd = config["resources_directory"],
              error_model = config["error_model"],
              upimapi_database = config["upimapi_database"],
              assembled = ' --assembled' if config['do_assembly'] else '',
              taxids = f' --taxids {config["upimapi_taxids"]}' if config["upimapi_database"] == 'taxids' else '',
              max_target_seqs = config["upimapi_max_target_seqs"],
              cols='&'.join(config['uniprot_columns']), dbs='&'.join(config['uniprot_databases']))

rule recognizer:
    input:
        expand("{output}/Annotation/{sample}/fgs.faa", output = config["output"], sample = set(exps["Sample"]))
    output:
        expand("{output}/Annotation/{sample}/reCOGnizer_results.xlsx", output = config["output"],
            sample = set(exps["Sample"]))
    threads:
        config["threads"] - 1
    run:
        shell("recognizer.py -f {input} -t {threads} -o {output}/Annotation/{sample} -rd {resources_directory}"
              "{download_cdd_resources} -dbs {recognizer_databases} -sd",
              output = config["output"], sample = set(exps["Sample"]),
              resources_directory = config["resources_directory"],
              recognizer_databases = config["recognizer_databases"],
              download_cdd_resources = '' if not config['download_cdd_resources'] else ' -dr')

rule quantification_analysis:
    input:
        expand("{output}/Preprocess/Trimmomatic/quality_trimmed_{name}{fr}.fq", output = config["output"],
            name = not_mp_exps["Name"],
            fr = (['_forward_paired', '_reverse_paired'] if exps["Files"].str.contains(',').tolist() else '')),
        expand("{output}/Assembly/{sample}/contigs.fasta", output = config["output"],
            sample = set(exps["Sample"])),
        expand("{output}/Annotation/{sample}/fgs.ffn", output = config["output"],
            sample = set(exps["Sample"]))
    output:
        expand("{output}/Quantification/{name}.readcounts", output = config["output"],
            name = set(not_mp_exps['Name'])),
    threads: config["threads"]
    run:
        for i in exps.index:
            if exps.iloc[i]['Data type'] == 'mrna':
                reference = f"{config['output']}/Annotation/{exps.iloc[i]['Sample']}/fgs.ffn"
            elif exps.iloc[i]['Data type'] == 'dna':
                reference = f"{config['output']}/Assembly/{exps.iloc[i]['Sample']}/contigs.fasta"
            else:
                continue
            perform_alignment(
                reference, [f"{config['output']}/Preprocess/Trimmomatic/"
                f"quality_trimmed_{exps.iloc[i]['Name']}_{fr}_paired.fq" for fr in ['forward', 'reverse']],
                f"{config['output']}/Quantification/{exps.iloc[i]['Name']}", threads=threads)
        generate_expression_matrix(
            [f"{config['output']}/Quantification/{mt_name}.readcounts" for mt_name in mt_exps['Name']],
            mt_exps['Name'].tolist(), f"{config['output']}/Quantification/expression_matrix.tsv")

rule metaphlan:
    input:
        expand("{output}/Preprocess/Trimmomatic/quality_trimmed_{name}{fr}.fq", output = config["output"],
            name = mg_exps["Name"],
            fr = (['_forward_paired', '_reverse_paired'] if exps["Files"].str.contains(',').tolist() else ''))
    output:
        expand("{output}/Taxonomy/{sample}_profiled_metagenome.txt", output = config["output"],
            sample = set(exps["Sample"]))
    threads: config["threads"]
    run:
        reads = ",".join(input)
        shell("metaphlan {reads} --bowtie2out {output}/Taxonomy/{sample}_mg.bowtie2.bz2 --nproc {threads} --input_type "
              "fastq",
              output = config["output"], sample = set(exps["Sample"]))
        shell("metaphlan {output}/Taxonomy/{sample}_mg.bowtie2.bz2 --nproc {threads} --input_type bowtie2out -o "
              "{output}/Taxonomy/{sample}_profiled_metagenome.txt",
              output = config["output"], sample = set(exps["Sample"]))

rule protein_report:
    input:
        expand("{output}/Annotation/{sample}/UPIMAPI_results.tsv", output = config["output"],
            sample = set(exps['Sample'])),
        expand("{output}/Annotation/{sample}/reCOGnizer_results.xlsx", output = config["output"],
            sample = set(exps["Sample"])),
        expand("{output}/Quantification/{name}.readcounts", output = config["output"],
            name = set(not_mp_exps['Name']))
    output:
        f"{config['output']}/MOSCA_Protein_Report.xlsx"
    threads: 1
    run: make_protein_report(config["output"], exps)

rule entry_report:
    input:
        f"{config['output']}/MOSCA_Protein_Report.xlsx"
    output:
        f"{config['output']}/MOSCA_Entry_Report.xlsx",
        expand("{output}/Quantification/{sample}/expression_matrix.tsv", output = config["output"],
            sample = set(exps["Sample"]))
    threads: 1
    run: make_entry_report(f"{config['output']}/MOSCA_Protein_Report.xlsx", config["output"], exps)

'''
rule entry_count:
    input:
        uniprotinfo=f"{config['output']}/Annotation/uniprotinfo.tsv",
        blasts=expand("{output}/Annotation/{name}/aligned.blast",output=config["output"], name=mg_exps['Name'].tolist())
    output:
        f"{config['output']}/MOSCA_Entry_Counts_Report.xlsx",
        f"{config['output']}/Quantification/expression_matrix.tsv"
    threads:
        1
    run:
        uniprotinfo = pd.read_csv(input.uniprotinfo[0], sep='\t')
        result = pd.DataFrame(columns=['sseqid'])
        i = 1
        names = []
        for blast in input.blasts:
            name = blast.split('/')[-2]
            print(f'[{i}/{len(input.blasts)}] Quantifying entries for: {blast}')
            data = parse_blast(blast).groupby('sseqid').size().reset_index(name=name)
            data['sseqid'] = [ide.split('|')[1] if ide != '*' else ide for ide in data['sseqid']]
            result = pd.merge(result, data, on='sseqid', how='outer')
            i += 1
            names.append(name)
        result.columns = ['Entry'] + result.columns.to_list()[1:]
        print(f'Merging entry counts with info at {input.uniprotinfo[0]}')
        result = pd.merge(result, uniprotinfo, on='Entry', how='left')
        multi_sheet_excel(f"{config['output']}/MOSCA_Entry_Counts_Report.xlsx", result, sheet_name='Sheet')
        result.to_csv(f"{config['output']}/MOSCA_Entry_Counts_Report.tsv", index=False, sep='\t')
        result[['Entry'] + names].to_csv(f"{config['output']}/Quantification/expression_matrix.tsv",
                                         sep='\t', index=False)
'''
rule differential_expression:
    input:
        expand("{output}/Quantification/{sample}/expression_matrix.tsv", output=config['output'],
               sample=set(exps["Sample"]))
    output:
        expand("{output}/Quantification/{{sample}}/condition_treated_results.tsv", output=config['output'])
    threads:
        1
    run:
        conditions = ",".join(map(str, mt_exps[mt_exps['Sample'] == wildcards.sample]['Condition'].tolist()))
        shell("Rscript {scripts_dir}/de_analysis.R --readcounts {input} --conditions {conditions} "
                  "--output {output}/Quantification/{wildcards.sample} --foldchange {minimum_fold_change}",
                  conditions = conditions, output = config["output"],
                  minimum_fold_change = config["minimum_differential_expression"])

rule keggcharter:
    input:
        f"{config['output']}/MOSCA_Entry_Report.xlsx"
    params:
        outdir="{output}/KEGG_maps/{sample}"
    output:
        "{output}/KEGG_maps/{sample}/KEGGCharter_results.tsv"
    threads:
        1
    run:
        shell("keggcharter.py -f {input} -o {params.outdir} -gcol {mg_cols} -tcol {exp_cols} -tc "
              "'Taxonomic lineage ({taxa_level})' -not {number_of_taxa} -keggc 'Cross-reference (KEGG)' "
              "-rd {resources_directory}{metabolic_maps} || true",
              mg_cols = ','.join(mg_exps['Name'].tolist()), resources_directory = config["resources_directory"],
              metabolic_maps = f" -mm {','.join(config['keggcharter_maps']) if len(config['keggcharter_maps']) > 0 else ''}",
              exp_cols = ','.join(mt_exps['Name'].tolist()), taxa_level = config["keggcharter_taxa_level"],
              number_of_taxa = config["keggcharter_number_of_taxa"])

rule report:
    input:
        f"{config['output']}/MOSCA_Protein_Report.xlsx",
        expand("{output}/Quantification/{sample}/condition_treated_results.tsv", output=config['output'],
               sample=set(exps["Sample"]))
    output:
        f"{config['output']}/technical_report.tsv",
        f"{config['output']}/MOSCA_General_Report.xlsx",
        f"{config['output']}/MOSCA_results.zip"
    threads:
        1
    run:
        shell("python {scripts_dir}/report.py -e {output}/exps.tsv -o {output} -ldir {reporter_lists} -if tsv{suffix}",
              output = config["output"], reporter_lists = f'{scripts_dir}/../resources',
              suffix = f' -s {config["suffix"]}' if config["suffix"] != '' else '')


